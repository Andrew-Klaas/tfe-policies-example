import "tfplan"

# Get an array of resource bodies of the given type (each body indexed by count),
# collected from all modules. (Returns an empty array if none found).
# Outline of structure:
# [ {"0": {"applied": {"instance_type": "..."}, "diff": {...}}, "1": {...}},
#   {"0": {...}},
#   ... ]
get_resources = func(type) {
	instances = []
	if length(tfplan.module_paths else []) > 0 { # always true in the real tfplan import
		for tfplan.module_paths as path {
			instances += values(tfplan.module(path).resources[type]) else []
		}
	} else { # fallback for tests
		instances += values(tfplan.resources[type]) else []
	}
	return instances
}

disallowed_cidr_blocks = [
	"0.0.0.0/0",
	"*",
]

security_groups = get_resources("azurerm_network_security_group")

block_allow_all = rule {
	all security_groups as _index, instances {
		all instances as _count, sg {
			all sg.applied.security_rule as _index, sr {
				(sr.source_address_prefix not in disallowed_cidr_blocks) or sr.access == "Deny"
			}
		}
	}
}

main = rule {
	(block_allow_all) else true
}